/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */

package ocl.monticoreocl;

grammar OCL extends siunit.monticoresiunit.SI, de.monticore.OCLExpressions2 {

	/*========================================================================*/
    /*============================ PRODUCTIONS  ==============================*/
    /*========================================================================*/

    /*====================== HEADER DEFINITIONS ==============================*/


	CompilationUnit =
		("package" package:(Name || ".")+ ";")?
		(ImportStatement)*
		OCLFile;

	/** ASTOCLFile represents a file that contains an OCL-Constraint.
        @attribute fileName
                   Name of OCLFile (necessary for MCCompilationUnit)
        @attribute OCLConstraints
                   List of OCL-constraints.
    */
    // Note: Prefix should be only "ocl" (-> Context Condition). On the
    //       other side this should not prevent using "ocl" as identifiers.
    //       Therefore this is not defined as terminal symbol.

	OCLFile =
        prefix:Name
        fileName:Name "{"
        OCLConstraint*
        "}"
        ;


    /** ASTOCLConstraint subsumes all OCL types that are used as embedded OCL.
        An OCLConstraint (Invariant or Method- or
        ConstructorSpec) or an abstract expression (expression).
    */
    interface OCLConstraint;


	/** ASTOCLOperationSignature subsumes method and constructor signatures.
    */
	interface OCLOperationSignature;

	OCLTaggedExpr = "<<" (Expression | OCLConstraint) ("," (Expression | OCLConstraint))* ">>" ;

	/** ASTOCLOperationConstraint represents the typical method or constructor
        specifications with pre- and post-conditions.
        @attribute OCLTaggedExpr
        		   is needed in cases like "<<special, author = "mustermann">> context...." , e.g. taggedvalues1.ocl
        @attribute OCLOperationSignature
                   Operation Signature (of a method or constructor).
        @attribute LetDeclaration
                   Optional let-declarations.
        @attribute OCLPreStatement
                   Optional pre-statements.
        @attribute OCLPostStatement
                   Optional post-statements.
    */
	OCLOperationConstraint implements OCLConstraint =
        OCLTaggedExpr?
        "context"
        OCLTaggedExpr?
        OCLOperationSignature
       	LetDeclaration?
        OCLPreStatement?
        OCLPostStatement?;

	/** ASTOCLInvariant represents the typical invariant definitions of OCL.
        @attribute OCLClassContext
        		   Optional keyword definitions: (context|import) and class definition
        @attribute name
                   Name of the invariant.
        @attribute OCLParameters
                   Optional parameters of the invariant.
        @attribute statements
                   List of boolean expressions.
    */
	OCLInvariant implements OCLConstraint =
        OCLClassContext?
        "inv" Name?
        OCLParameters?
         ":"
        (statements:Expression ";")+
        ;

    /** ASTOCLClassContext represents the OCL-Context-Clause of an invariant.
        @attribute contextType
                   Type of context (context or import)
                   For import look at http://mbse.se-rwth.de/book1/index.php?c=chapter3-1#x1-400003.1.1
    */
    OCLClassContext =
    	(context:["context"] | Import:["import"])
    	(OCLContextDefinition || ",")+
    	;

    /** ASTOCLContextDefinition can be of the followoing type.
        Auction | Auction a | a in (...) | Auction a in (...)
        The rule is written as follows to avoid matching empty strings
    */
	OCLContextDefinition =
		"types2sd" Type (varNames:(Name || ",")* ("in" Expression)?)? |
		varNames:(Name || ",")+ ("in" Expression)
	    ;

	/** ASTOCLPreStatement represents a list of pre-statements subsumed to a
        pre-condition.
        @attribute name
                   Optional name of pre-condition.
        @attribute statements
                   List of boolean expressions.
    */
	OCLPreStatement  =
        "pre" Name? ":"
        (statements:Expression+ ";")+;

    /** ASTOCLPostStatement represents a list of post-statements subsumed to a
        post-condition.
        @attribute name
                   Optional name of post-conditions.
        @attribute statements
                   List of boolean expressions.
    */
    OCLPostStatement  =
        "post" Name? ":"
        (statements:Expression+ ";")+;

	/** ASTOCLMethodSignature defines the signature of a Java5 method.
        @attribute ReturnType
                   Optional complex type that is returned by given method
        @attribute methodName
                   Name of given method.
        @attribute OCLParameters
                   Parameter declarations of given method.
        @attribute OCLThrowsClause
                   Optional throwables.

        Example : context Person.addMessages(Message m):
    */
	OCLMethodSignature implements OCLOperationSignature =
        ReturnType?
        methodName:QualifiedName
        OCLParameters
        OCLThrowsClause?;

    /** ASTOCLConstructorSignature defines signature of a constructor.
        @attribute ReferenceType
                   Type of the Constructor.
        @attribute OCLParameters
                   Parameters of constructor.
        @attribute OCLThrowsClause
                   Optional throwables.
    */
    OCLConstructorSignature implements OCLOperationSignature =
        "new" referenceType:Name
        OCLParameters
        OCLThrowsClause?;


    /** ASTOCLParameters defines a list of parameter declarations (can also be
        empty)
    */
    OCLParameters =
        "(" params:(OCLParameterDeclaration || ",")* ")";

    OCLParameterDeclaration  =
        Type Name;

	/** ASTOCLThrowsClause defines throwables for a method or constructor
        signature.
        @attribute throwables
                   List of throwables.
    */
    OCLThrowsClause =
        "throws" throwables:(Name || ",")+;


    /*============================== DECLARATORS ============================*/

 	/** ASTOCLDeclaration defines declarations of variables and methods, that
        are used in let-constructs.
    */
 	interface OCLDeclaration;
    EDeclaration = OCLDeclaration;

    /** ASTOCLMethodDeclaration defines internal helping methods in let
        constructs, e.g. "max(int a, int b) = if (a>b) then ...".
        @attribute ReturnType
                   Optional returntype of method.
        @attribute name
                   Name of method.
        @attribute OCLParameters
                   List of parameters (can also be emtpy).
        @attribute Expression
                   Method definition as an expression (right hand of the
                   assignment).
    */
    symbol OCLMethodDeclaration implements OCLDeclaration, OCLComprehensionItem =
        ReturnType?
        Name
        OCLParameters
        "=" Expression;

 	 /** ASTOCLVariableDeclaration defines a local variable in a let declaration
        or a comprehension.
        @attribute Type
                   Optional type.
        @attribute name
                   Name of the variable.
        @attribute Expression
                   Definition as an expression (right hand of the assignment).
     */
     OCLVariableDeclaration implements OCLDeclaration, OCLComprehensionItem =
        Type?
        Name
        "=" Expression;


    /*============================= OCL PRIMARYS =============================*/

    /** ASTOCLPrimary defines primaries of OCL.
    */
    interface OCLPrimary;

    OCLParenthizedPrimary implements OCLPrimary =
        "(" Expression | OCLPrimary ")"
        ("." qualification:OCLPrimary)?;

    /** ASTOCLNumberLiteral and NonNumberLiteral are defined here to overwrite and implement numbers from SI grammar
        to support expressions as 7 m/s
    */
    OCLNonNumberPrimary implements OCLPrimary =
        value:Literal;

    OCLNumberPrimary implements OCLPrimary =
        value:Number;

    OCLIsNewPrimary implements OCLPrimary =
        "isnew" "(" Expression ")";

    OCLDefinedPrimary implements OCLPrimary =
        "defined" "(" Expression ")";

    /** ASTOCLQualifiedPrimary represents qualified identifier.
        @attribute prefixIdentifier
                   Value of first part of a qualified identifier. It is null if
                   we have this, super or result as prefix.
        @attribute this
                   Is true if we have a this prefix.
        @attribute super
                   Is true if we have a super prefix.
        @attribute result
                   Is true if we have a result prefix.
        @attribute qualifications
                   List of qualifications of qualified identifier.
        @attribute postfixQualification
                   Optional argument-, array- or atpre, **-qualifcation.
        @attribute OCLQualifiedPrimary
                   Optional recursive linkage of qualified primary.

        example: prefix.name.name(argument).nextQualifiedPrimary
                 prefix.name[expr]
                 prefix**
                 prefix@pre
    */
    OCLQualifiedPrimary implements OCLPrimary =
        (
            this:["this"]
            |
            super:["super"]
            |
            res:["result"]
            |
            prefixIdentifier:Name
        )

        ("." qualifications:Name)*
        postfixQualification:OCLQualification?
        ("." OCLQualifiedPrimary)?
        ;


    /** ASTOCLQualification defines qualification postfixes of a qualified
        primary. A qualification is one of the three kinds:
            - x[0] (ArrayQualification)
            - x(y) (ArgumentQualification)
            - x** or xAtpre (PostfixQualification)
    */
    interface OCLQualification;

    /** ASTOCLArrayQualification represents an array qualification.
        @attribute arguments
            The array arguments (indices).
    */
    OCLArrayQualification implements OCLQualification=
        ("[" arguments:Expression "]")+
        ;

    /** ASTOCLArgumentQualification represents the arguments of a method call.
        @attribute arguments
            Arguments of method call.
    */
    OCLArgumentQualification implements OCLQualification =
        "(" (Expression || "," )* ")"
        ;

    /** ASTOCLPostfixQualification represents a ATpre qualification.
        @attribute OCLQualification
                   Optional qualification.
    */
    OCLAtPreQualification implements OCLQualification =
        atpre:["@pre"]
        OCLQualification?;

    /** ASTOCLPostfixQualification represents a ** qualification.
        @attribute OCLQualification
                   Optional qualification.
    */
    OCLPostfixQualification implements OCLQualification =
        transitive:["**"]
        OCLQualification?;

    /*============================= OCL Comprehensions =============================*/

    /** ASTOCLComprehensionPrimary represents comprehensions.
        @attribute Type
                   Optional type of comprehension, e.g. Set, List or Collection.
        @attribute referenceParamter
                   Optional reference type of type
        @attribute expression
                   Expression of comprehension.
        @attribute qualification
                   Optional Qualification.
    */
    OCLComprehensionPrimary implements OCLPrimary =
        (container:["Set" | "List" | "Collection"]
        ("<" referenceParameter:Type ">")?)?
        "{" expression:OCLComprehensionExpr? "}"
        ("." qualification:OCLPrimary)?
        ;

    /** ASTOCLComprehensionExpr defines comprehension kinds.
        e.g.:
            - {x in y | x > 0} (OCLComprehensionVarDeclaratorStyle)
            - {x * x | x in y} (OCLComprehensionExpressionStyle)
            - {1..3, x..10, y} (OCLComprehensionEnumerationStyle)
    */
    interface OCLComprehensionExpr;

    /** ASTOCLComprehensionVarDeclaratorStyle defines a comprehension with given
        characteristic.
        @attribute generator
                   A collection declaration
        @attribute comprehensionItems
                   Characterization of comprehension as a list of
                   comprehension-items.
    */
    OCLComprehensionVarDeclaratorStyle implements OCLComprehensionExpr <10> =
    	generator:OCLInExpr "|"
    	(OCLComprehensionItem || ",")+;

 	/** ASTOCLComprehensionExpressionStyle defines a comprehension with given
        characteristic.
        @attribute expression
                   An expression (e.g. "x*x").
        @attribute comprehensionItems
                   Characterization of comprehension as a list of
                   comprehension-items. This can be generators, vardefinitions
                   or filters. Note that we assume at least one generator
                   (e.g. x in Y) in here.
    */
    OCLComprehensionExpressionStyle implements OCLComprehensionExpr <20> =
    	Expression "|"
    	(OCLComprehensionItem || ",")+
    	;

    /** ASTOCLComprehensionEnumerationStyle is used for an enumeration of
        comprehension elements. Note that collection items are optional.
        @attribute collectionItems
                   Enumerated elements as a list separated by , (comma).
                   (e.g.: "1..3, x, y..z")
    */
    OCLComprehensionEnumerationStyle implements OCLComprehensionExpr <30> =
    	(OCLCollectionItem || ",")+
    	;

    /** ASTOCLComprehensionItem defines items of a comprehension.
    */
    interface OCLComprehensionItem;

    /** ASTOCLInExpr defines a collection like "int x in y" or "Auction a" as
        shortform of "Auction a in Auction.allInstances").
    */
    EInExpr = OCLInExpr;
    OCLInExpr implements OCLComprehensionItem =
		Type?
		varNames:(Name || ",")+
		("in" expression:Expression)?
        ;

    /** ASTOCLCollectionItem is a single comprehension item or a valuation,
        e.g. {1..5}
    */
    OCLCollectionItem =
        (Expression  ".."  Expression)
        |
        Expression
        ;

}
